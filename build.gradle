plugins {
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
    group 'design.lmao.lmaolib'
    version '1.0.0'

    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    ext {
        // dependency versions
        kotlinVersion = '1.6.0'

        getCurrentShortRevision = {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    executable = "git"
                    args = ["rev-parse", "HEAD"]
                    standardOutput = os
                }
                return os.toString().trim().substring(0, 8)
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url 'https://jitpack.io'
        }
    }

    dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-stdlib'

        // TODO: replace with dependency version constants
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

        compileOnly 'com.google.code.gson:gson:2.8.9'

        // We'll shade this into each platform, keep it compileOnly in parent
        compileOnly "com.github.growlyx:flavor:0.1.2"
    }


    compileKotlin {
        kotlinOptions.javaParameters = true
        kotlinOptions.jvmTarget = "1.8"
    }

    shadowJar {
        archiveClassifier.set("")

        exclude "**/*.kotlin_metadata"
        exclude "**/*.kotlin_builtins"
        exclude "META-INF/"

        archiveFileName = "lmaolib-${project.name}.jar"
    }

    publishing {
        publications {
            shadow(MavenPublication) { publication ->
                project.shadow.component(publication)
            }
        }
    }

    tasks.build.dependsOn(shadowJar)
}
